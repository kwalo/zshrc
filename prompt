# vim: filetype=zsh
#
# Kwalo ZSH Theme
# Author: Krzysztof Walo
#
# Based on Sobole ZSH Theme by Nikita Sobolev
# https://github.com/sobolevn/sobole-zsh-theme
#

# ----------------------------------------------------------------------------
# PROMPT settings
# These settings changes how your terminal prompt looks like
# ----------------------------------------------------------------------------

PROMPT='
$(current_dir)${_jobs_status}$(git_prompt_info)
${_current_caret} '

PROMPT2='. '

# Display number of background jobs in magenta
_jobs_status="%(1j. %F{magenta}%j j%f .)"


function current_dir {
  # Settings up current directory and settings max width for it:
  local _max_pwd_length=$(($COLUMNS * 80 / 100))

  if [[ $(echo -n $PWD | wc -c) -gt ${_max_pwd_length} ]]; then
    echo "%B%F{white}%-2~ ... %3~%f%b "
  else
   echo "%B%F{white}%~%f%b "
  fi
}

function git_prompt_info {
  local ref=$(git symbolic-ref --short HEAD 2> /dev/null) || return 0
  local GIT_PROMPT_PREFIX="%F{green}"
  local GIT_PROMPT_SUFFIX="%f"
  echo "${GIT_PROMPT_PREFIX}${ref}${GIT_PROMPT_SUFFIX}"
}

function zle-line-init zle-keymap-select {
  case ${KEYMAP} in
    vicmd)      VI_MODE_CHAR=":" ;;
    *) VI_MODE_CHAR="$" ;;
  esac
  _current_caret="%(?.%F{white}${VI_MODE_CHAR}%f.%F{red}${VI_MODE_CHAR}%f)"
  zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

case $TERM in
  st*|xterm*)
    precmd () { print -Pn "\e]0;%~\a" }
esac

